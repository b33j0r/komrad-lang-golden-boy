agent WsHandler {
	name = "Unknown"

	[ws _socket connected] {
		msg = dict {
			name = "System"
			message = "WsHandler connected"
		}
		msg_json = json encode msg
		socket send msg_json
	}

	[ws _socket disconnected] {
		Io println "Disconnected: " + socket
	}

	[ws _socket text _message_json] {
		message = json decode message_json
		server broadcast name message
		1337
	}

	[send _message] {
		Io println "Sending: " + message
		socket send message
	}
}

agent Server {


	[http _request _response GET] {
		Io println "GET /"
		templates = spawn Tera {
			base_dir = "./examples/templates"
		}
		rendered = templates render "chat.html" {
		  title = "Komrad"
		  count = count
		}
		response html rendered
	}

	[ws _socket connect] {
		client = spawn WsHandler {
			server = me
		}
		messages foreach x {
			x_json = json encode x
			socket send x_json
		}
		socket set-delegate client
		clients add client
	}

	[broadcast _sender _message] {
		messages add message
		clients foreach x {
			x send message
		}
	}

	[http _request _response GET "favicon.ico"] {
		Io println "GET /favicon.ico"
		content = Fs read-all-binary "./examples/static/tape.ico"
		response binary content
	}
}

[main] {
	server = spawn Server {
		clients = []
		messages = []
	}
	listener = spawn HyperListener {
		host = "0.0.0.0"
		port = 9898
		delegate = server
	}
}
